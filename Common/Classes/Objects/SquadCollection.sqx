/*
 * Name:	SquadCollection
 * Date:	2018-11-17
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * Collection of squads.
 */
namespace Intrusion.Common
{
	public class SquadCollection
	{
		private fields ["_mLockedById" as Scalar];
		
		// Creates a SquadCollection object.
		public constructor()
		{
			_self.Squads = [];
			_mLockedById = 0;
		};
		
		// Gets the squad array.
		public property Array Squads { get; private set; };
		
		// Adds a new or updates an existing squad.
		// _squad (Squad): The squad to add or update.
		public method AddOrUpdateSquad("_squad" as Squad)
		{
			scopeName "main";

			for "_i" from 0 to (count _self.Squads - 1) do 
			{
				private ["_existingSquad" as Squad];
				
				_existingSquad = _self.Squads select _i;
				
				if (_existingSquad.Group == _squad.Group) then {
					_self.Squads set [_i, _squad];
					breakOut "main";
				};
			};
			
			_self.Squads pushBack _squad;
		};
		
		// Removes all empty squads.
		public method RemoveEmptySquads {
			private ["_squadsToKeep" as Array];
			
			_squadsToKeep = [];
		
			{
				if (!(call _x.IsEmpty)) then {
					_squadsToKeep pushBack _x;
				};
			} foreach _self.Squads as Squad;
			
			_self.Squads = _squadsToKeep;
		};
		
		// Gets the number of squads with a profession type of a certain side.
		// _side (Side): The side to count.
		// _professionType (ProfessionType): The profession type to count.
		public method CountByProfessionType("_side" as Side, "_professionType" as ProfessionType)
		{
			[(str _this) + " SquadCollection.CountByProfessionType"] call LogHandler.Debug;
			{ side _x.Group == _side && _x.ProfessionType == _professionType } count (_self.Squads) as Squad;
		};
		
		// Checks whether a profession type is available for a squad to select.
		// _side (Side): The side to check.
		// _professionType (ProfessionType): The profession type to check.
		// Returns (Boolean): true if squads can select the profession, false if no mor squads of the 
		// current profession can be assigned.
		public method Boolean ProfessionTypeAvailable("_side" as Side, "_professionType" as ProfessionType)
		{
			private ["_assignedSquadsCount" as Scalar, "_profession" as Profession];
			[(str _this) + " SquadCollection.ProfessionTypeAvailable"] call LogHandler.Debug;
			
			_assignedSquadsCount = [_side, _professionType] call _self.CountByProfessionType;
			_profession = [_professionType] call gCommonConfig.Professions.GetProfession;
			
			_assignedSquadsCount < _profession.MaxGroupsCount || _profession.MaxGroupsCount < 0
		};
		
		// Aquires a lock that prevents other threads to change the squads simultaneously.
		public method AquireLock {
			// Check that no other thread is currently making changes to the squads.
			while { _mLockedById != 0 } do { sleep 0.01; };
			_mLockedById = 1;
		};
		
		// Releases the lock set by AquireLock so that other threads can make changes to squads.
		public method ReleaseLock {
			_mLockedById = 0;
		};
		
		// Gets a squad by a group.
		// _group (Group): The group to get squad for.
		// Returns (Squad): The group's squad. classNull if the squad was not found.
		public method Squad GetSquadByGroup("_group" as Group)
		{
			scopeName "main";
		
			{
				private ["_squad" as Squad];
				_squad = _x as Squad;
				
				if (_squad.Group == _group) then {
					_squad breakOut "main";
				};
			} foreach _self.Squads as Squad;
			
			classNull
		};
	};
};
