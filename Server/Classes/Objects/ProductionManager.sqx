/*
 * Name:	ProductionManager
 * Date:	2017-11-19
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * A class modeling a side's production manager, refining generated production (i.e. spawns new
 * vehicles and weapons).
 */

using Intrusion.Common;
using Intrusion.Communication;

namespace Intrusion.Server
{
	public class ProductionManager : IGroupChangingProfessionListener
	{
		private fields ["_mProfessionVehiclesConfig" as IProfessionVehiclesConfig, "_mVehicleClassNamesConfig" as IVehicleClassNamesConfig, "_mVehicleSpawner" as IVehicleSpawner, "_mResourceBook" as ResourceBook, "_mReservationQueue" as ReservationQueue, "_mSquadHandler" as ISquadHandler, "_mScientist" as IScientist, "_mMessageHandler" as IMessageHandler, "_mProfessionConfig" as IProfessionConfig];

		// Creates a ProductionManager object.
		public constructor("_professionVehiclesConfig" as IProfessionVehiclesConfig, "_vehicleClassNamesConfig" as IVehicleClassNamesConfig, "_side" as Side, "_vehicleSpawner" as IVehicleSpawner, "_resourceBook" as ResourceBook, "_squadHandler" as ISquadHandler, "_scientist" as IScientist, "_messageHandler" as IMessageHandler, "_professionConfig" as IProfessionConfig, "_reservationQueue" as ReservationQueue)
		{
			_mProfessionVehiclesConfig = _professionVehiclesConfig;
			_mVehicleClassNamesConfig = _vehicleClassNamesConfig;
			_self.Side = _side;
			_mVehicleSpawner = _vehicleSpawner;
			_mSquadHandler = _squadHandler;
			_mResourceBook = _resourceBook;
			_mScientist = _scientist;
			_mMessageHandler = _messageHandler;
			_mProfessionConfig = _professionConfig;
			_mReservationQueue = _reservationQueue;
			[_self] call SquadHandler.RegisterGroupChangingProfessionListener;

/*
			// If reservationQueue is null, then we are in production mode (not in unit testing)
			if (isNull _mReservationQueue) then {
				_mReservationQueue = new ReservationQueue;
			
				// Register the class to the global group handler to recieve notifications 
				// when groups on the side is changing its profession.
				[_self] call SquadHandler.RegisterGroupChangingProfessionListener;
			};
*/
		};
		
		// Gets the production manager's side.
		public property Side Side { get; private set; };
		
		// Creates a vehicle reservation for a group
		// _group (Group): The group to create reservation for.
		// _professionType (ProfessionType): The profession type.
		private method CreateReservation("_group" as Group, "_professionType" as ProfessionType)
		{
			private ["_reservation" as Reservation, "_vehicleClassNamesInfo" as ProfessionVehicleClassNamesInfo, "_vehicleClassName" as String, "_professionVehicle" as ProfessionVehicleInfo];

			// Remove the group's eventual existing reservations.
			[_group] call _mReservationQueue.RemoveReservationsByGroup;
			
			_vehicleClassNamesInfo = [side _group, _professionType] call _mVehicleClassNamesConfig.GetProVehiclesClassNameInfo;
			//_vehicleClassNames = [side _group, _professionType] call VehicleClassNamesConfig.GetProVehiclesClassNames; // TODO SQX (TEST ADDED): Must generate error that VehicleClassNamesConfig is not static.

			// Create an reservation if there is a vehicle to assign.
			if (!isNull _vehicleClassNamesInfo) then {
				_vehicleClassName = selectRandom _vehicleClassNamesInfo.ClassNames;
				_professionVehicle = [_professionType] call _mProfessionVehiclesConfig.GetVehicle;
				
				_reservation = [_group, _vehicleClassName, _professionVehicle.Cost] new Reservation;
				[_reservation] call _mReservationQueue.PutReservation;
				
//				if (call _mReservationQueue.Count == 1) then {
//					[_mReservationQueue.CurrentReservation.Group] call _self.TellSquadVehicleSoonDelivered;
//				};
			};
		};
		
		// Called when a group on the production manager's side changes its profession.
		// _group (Group): The group that is changing its profession.
		// _professionType (ProfessionType): The group's new profession type.
		public method OnGroupChangingProfession("_group" as Group, "_professionType" as ProfessionType)
		{
			private ["_cost" as Scalar];
			
			// Delete the group's eventual existing vehicle and return its production value.
			_cost = [_group] call _mVehicleSpawner.DeleteProfessionVehicles;
			[_cost] call _mResourceBook.DepositProduction;
			
			[_group, _professionType] call _self.CreateReservation;
		};
		
//		private method TellSquadVehicleSoonDelivered("_group" as Group)
//		{
//			["Your squad's vehicle is in the production queue and will soon be delivered.", _group] call _mMessageHandler.ShowMessage;
//		};
		
		public method AddReservationsForSquadsWithNoVehicle()
		{
			private ["_intrusionVehicles" as Array /* of IntrusionVehicle */];
			private ["_squadCollection" as SquadCollection, "_squad" as Squad, "_reservation" as Reservation];
			
			call _mVehicleSpawner.RemoveOwnershipOfDestroyedVehicles;
			
			_squadCollection = call PublicVariableHandler.GetSquadCollection;
			
			{
				_squad = _x;

				if (side _squad.Group == _self.Side) then {
					_reservation = [_squad.Group] call _mReservationQueue.GetReservationByGroup;
					
					if (isNull _reservation) then {
						_intrusionVehicles = [_squad.Group] call _mVehicleSpawner.GetIntrusionVehiclesByGroup;
		
						if (count _intrusionVehicles == 0) then {
							[_squad.Group, _squad.ProfessionType] call _self.CreateReservation;
						};
					};
				};
			} foreach _squadCollection.Squads as Squad;
		};
		
		// Performs a cleanup and "unproduces" all profession vehicles that do not have an owner. Production spent on the
		// vehicle is returned to the side.
		public method DeleteProfessionVehiclesWithNoOwner()
		{
			private ["_cost" as Scalar];

			_cost = [_self.Side] call _mVehicleSpawner.DeleteProfessionVehiclesWithNoOwner;
			[_cost] call _mResourceBook.DepositProduction;
		};
		
		// Produces vehicles from the production queue.
		public method PerformProduction {
			private ["_tryOneMore" as Boolean, "_squad" as Squad, "_technologyInvented" as Boolean];
			private ["_profession" as Profession, "_vehiclesCount" as Scalar];
			private ["_totalProduction" as Scalar, "_cost" as Scalar];

			["ProductionManager.PerformProduction (" + str _self.Side + "):"] call LogHandler.Debug;
			["_mReservationQueue: " + str _mReservationQueue] call LogHandler.Debug;
			["_mSquadHandler: " + str _mSquadHandler] call LogHandler.Debug;

			_tryOneMore = true;

			["_tryOneMore: " + str _tryOneMore] call LogHandler.Debug;
			["_mReservationQueue.CurrentReservation: " + str _mReservationQueue.CurrentReservation] call LogHandler.Debug;
			while { _tryOneMore && {!isNull _mReservationQueue.CurrentReservation} } do
			{
				["while { _tryOneMore && {!isNull _mReservationQueue.CurrentReservation} } do"] call LogHandler.Debug;
				_totalProduction = _mResourceBook.TotalProduction;
				_cost = _mReservationQueue.CurrentReservation.ProductionCost;
				
				_tryOneMore = false;
				_squad = [_mReservationQueue.CurrentReservation.Group] call _mSquadHandler.GetSquadByGroup;
				
				["_totalProduction: " + str _totalProduction] call LogHandler.Debug;
				["_cost: " + str _cost] call LogHandler.Debug;
				["_tryOneMore: " + str _tryOneMore] call LogHandler.Debug;
				["_squad: " + str _squad] call LogHandler.Debug;
			
				if (!isNull _squad) then
				{
					["if (!isNull _squad) then"] call LogHandler.Debug;
					
					_profession = [_squad.ProfessionType] call _mProfessionConfig.GetProfession;
					["_profession: " + str _profession] call LogHandler.Debug;
					
					_technologyInvented = [_profession.RequiredTechnology] call _mScientist.TechnologyIsInvented;
					["_technologyInvented: " + str _technologyInvented] call LogHandler.Debug;
					
					if (!_technologyInvented) then
					{
						["if (!_technologyInvented) then"] call LogHandler.Debug;
						call _mReservationQueue.MoveCurrentReservationLast;
					}
					else {
						["else (if _technologyInvented)"] call LogHandler.Debug;
						if (_profession.MaxUnitsPerVehicle <= 0) then {
							_vehiclesCount = 1;
						}
						else {
							_vehiclesCount = ceil ((count units _squad.Group) / (_profession.MaxUnitsPerVehicle));
						};

						["_vehiclesCount: " + str _vehiclesCount] call LogHandler.Debug;
						
						if (_totalProduction >= _cost * _vehiclesCount && _technologyInvented) then
						{
							["if (_totalProduction >= _cost * _vehiclesCount && _technologyInvented)"] call LogHandler.Debug;
							
							// Create the vehicle(s)
							for "_i" from 1 to _vehiclesCount do
							{
								["for '_i' from 1 to _vehiclesCount do"] call LogHandler.Debug;
								[_squad.Group, _squad.ProfessionType, _mReservationQueue.CurrentReservation.VehicleClassName, _cost] call _mVehicleSpawner.SpawnProfessionVehicle;
								[_cost] call _mResourceBook.WithdrawProduction;
							};

							// Tell the group
							["Your squad's vehicle is ready and waiting for you at the base.", _mReservationQueue.CurrentReservation.Group] call _mMessageHandler.ShowMessage;
							
							// Remove the reservation
							call _mReservationQueue.RemoveCurrentReservation;

							// Tell the next squad that their vehicle is being produced.
							if (call _mReservationQueue.Count > 0) then
							{
								["if (call _mReservationQueue.Count > 0)"] call LogHandler.Debug;
								//[_mReservationQueue.CurrentReservation.Group] call _self.TellSquadVehicleSoonDelivered;
							};
							
							_tryOneMore = true;
						};
					};
				};
			};
		};
	};
};
