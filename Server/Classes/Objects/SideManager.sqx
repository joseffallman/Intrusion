/*
 * Name:    SideHandler
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * Manages the resource collection for a side.
 */

using Intrusion.Common;

namespace Intrusion.Server
{
	public class SideManager
	{
		private fields [
			"_mResourceBook" as ResourceBook,
			"_mResourceCollector" as ResourceCollector,
			"_mProductionManager" as ProductionManager,
			"_mScientist" as Scientist,
			"_mReservationQueue" as ReservationQueue,
			"_mSquadHandler" as ISquadHandler,
			"_mProfessionConfig" as IProfessionConfig];
		
        // Creates a SideHandler object.
        // Parameter(s):
        // _side (Side): The side to handle.
        public constructor(
			"_serverConfig" as IServerConfig, 
			"_commonConfig" as ICommonConfig, 
			"_side" as Side, 
			"_vehicleSpawner" as IVehicleSpawner, 
			"_squadHandler" as ISquadHandler, 
			"_messageHandler" as IMessageHandler, 
			"_inventedTechnologiesCollection" as InventedTechnologiesCollection,
			"_unitCounter" as IUnitCounter,
			"_professionConfig" as IProfessionConfig,
			"_matchLengthMinutes" as Scalar)
		{
            _self.Side = _side;
            _mSquadHandler = _squadHandler;
            _mProfessionConfig = _professionConfig;
            
            _mResourceBook = new ResourceBook;
            _mResourceCollector = [new MissionTime, _serverConfig.Gameplay, _matchLengthMinutes, _side, _mResourceBook, _unitCounter] new ResourceCollector;
            _mScientist = [_side, _mResourceBook, _commonConfig.Technology, _messageHandler, _inventedTechnologiesCollection] new Scientist;
            _mReservationQueue = new ReservationQueue;
            _mProductionManager = [_commonConfig.ProfessionVehicles, _commonConfig.VehicleClassNames, _side, _vehicleSpawner, _mResourceBook, _squadHandler, _mScientist, _messageHandler, _commonConfig.Professions, _mReservationQueue] new ProductionManager;
		};
		
		// Gets the side of the side handler.
		public property Side Side { get; private set; };
		
		private method Boolean SquadsTechnologyInvented("_group" as Group)
		{
			private ["_profession" as Profession, "_technologyInvented" as Boolean, "_squad" as Squad];
			
			_technologyInvented = false;
			_squad = [_mReservationQueue.CurrentReservation.Group] call _mSquadHandler.GetSquadByGroup;
			
			if (!isNull _squad) then {
				_profession = [_squad.ProfessionType] call _mProfessionConfig.GetProfession;
				_technologyInvented = [_profession.RequiredTechnology] call _mScientist.TechnologyIsInvented;
			};
			
			_technologyInvented
		};
		
		// Performs a turn for the side. I.e. collects resources and creates vehicles etc.
		public method ResourceReport PerformTurn {
			private ["_factoryCount" as Scalar, "_techLabCount" as Scalar, "_nextVehicleGroup" as Group, "_nextVehicleCost" as Scalar];
			private ["_resourceReport" as ResourceReport];
		
			call _mResourceCollector.CollectResources;
			call _mScientist.PerformResearch;
			call _mProductionManager.AddReservationsForSquadsWithNoVehicle;
			call _mProductionManager.PerformProduction;
			
			_factoryCount = [_self.Side] call ResourceLocationHandler.GetFactoriesCountBySide;
			_techLabCount = [_self.Side] call ResourceLocationHandler.GetTechLabsCountBySide;
			
			_nextVehicleGroup = grpNull;
			_nextVehicleCost = 0;
			
			if (!isNull _mReservationQueue.CurrentReservation) then
			{
				if ([_mReservationQueue.CurrentReservation.Group] call _self.SquadsTechnologyInvented) then {
					_nextVehicleGroup = _mReservationQueue.CurrentReservation.Group;
					_nextVehicleCost = _mReservationQueue.CurrentReservation.ProductionCost;
				};
			};
			
			_resourceReport = [_self.Side, _factoryCount, _techLabCount, _mResourceBook.TotalProduction, _mResourceBook.TotalResearch, _nextVehicleGroup, _nextVehicleCost] new ResourceReport;
			["_resourceReport: (" + str _self.Side + "): " + str _resourceReport] call LogHandler.Debug;
			
			_resourceReport
		};
	};
};
