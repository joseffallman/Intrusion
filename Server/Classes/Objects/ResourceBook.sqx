/*
 * Name:	ResourceBook
 * Date:	2017-11-19
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * Class modeling a resource book for a side.
 */
namespace Intrusion.Server
{
	public class ResourceBook
	{
		// Creates a ResourceBook object.
		public constructor {
			_self.TotalProduction = 0;
			_self.TotalAdministration = 0;
		};
		
		// Gets the total available production.
		public property Scalar TotalProduction { get; private set; };
		
		// Gets the total available administration.
		public property Scalar TotalAdministration { get; private set; };
		
		// Makes a deposito into the resource account.
		// _amount (Scalar): The amount to deposit.
		public method DepositProduction {
			params ["_amount" as Scalar];
			_self.TotalProduction = _self.TotalProduction + _amount;
		};
		
		// Makes a deposit into the administration account.
		// _amount (Scalar): The amount of administration to deposit.
		public method DepositAdministration {
			params ["_amount" as Scalar];
			_self.TotalAdministration = _self.TotalAdministration + _amount;
		};
		
		// Makes a withdrawal from the resource account.
		// _amount (Scalar): The amount to withdraw.
		// Returns (Boolean): true if the transaction was made. false if there were not enough on the account.
		public method Boolean WithdrawProduction {
			params ["_amount" as Scalar];
			private _success = false;
			
			if (_self.TotalProduction >= _amount) then {
				_self.TotalProduction = _self.TotalProduction - _amount;
				_success = true;
			};
			
			_success
		};
		
		// Prints the resource book to a readable text.
		// Retursn (String): A text string.
		public method String ToString {
			"Production: " + str _self.TotalProduction + ", Administraton: " + str _self.TotalAdministration
		};
	};
};
