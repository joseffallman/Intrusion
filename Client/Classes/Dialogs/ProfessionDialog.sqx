/*
 * Name:	ProfessionDialog
 * Date:	2017-11-29
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * A dialog in which the user can change profession.
 */

using Intrusion.Common;
using Intrusion.Communication;

namespace Intrusion.Client
{
	public class ProfessionDialog
	{
		private static fields ["_mOptions" as Array /* of ProfessionType */, "_mProfessionConfig" as IProfessionConfig];
	
		// Initializes the profession dialog.
		public static method Init("_professionConfig" as IProfessionConfig)
		{
			_mProfessionConfig = _professionConfig;
			_mOptions = [];
			_self.IsOpen = false;
			_self.SelectedProfession = ProfessionType.Undefined;
		};
		
		// Gets or sets the profession type that the user has selected.
		public static property ProfessionType SelectedProfession { get; set; };
		
		// Gets whether the dialog is open or not.
		public static property Boolean IsOpen { get; private set; };
		
		// Reads the selected profession type from the combo box
		// Returns (Profession): The selected profession. classNull if no profession is selected.
		private static method ProfessionType GetSelectedProfessionTypeFromCombo()
		{
			private _selectedIndex = lbCurSel (DialogControl.GroupProfessionCombo as Scalar);
			
			_mOptions select _selectedIndex
		};

		// Adds all professions to the combo box.
		private static method AddProfessions()
		{
			private ["_options" as Array, "_sGroupSize" as String, "_sMaxGroupsCount" as String];
			
            _options = [];
            
			{
				_sGroupSize = str _x.MinGroupSize + "-" + str _x.MaxGroupSize;
				if (_x.MinGroupSize == _x.MaxGroupSize) then {
					_sGroupSize = str _x.MinGroupSize;
				};
				
				_sMaxGroupsCount = "";
				if (_x.MaxGroupsCount >= 0) then {
					_sMaxGroupsCount = ", max " + str _x.MaxGroupsCount + " groups";
				};
				
				lbAdd [DialogControl.GroupProfessionCombo, (_x.Name + " (" + _sGroupSize + " members" + _sMaxGroupsCount + ")")];
				_options pushBack _x.Type;
			} foreach (call _mProfessionConfig.GetAllProfessions) as Profession;
            
            _mOptions = _options;
		};
	
		// Shows the dialog.
		// Returns (DialogResult): A dialog result.
		public static method DialogResult ShowDialog()
		{
			waitUntil { !dialog };
			
			// Create the dialog
            createDialog "IntProfessionDialog";
			
			call _self.AddProfessions;
			lbSetCurSel [DialogControl.GroupProfessionCombo, 0];
			
			_self.IsOpen = true;

			// Wait until dialog is closed
			waitUntil { !dialog };
			
			// Return OK.
			DialogResult.Ok
		};
		
		// Closes the dialog if it is open.
		public static method CloseDialog()
		{
			if (_self.IsOpen) then {
				closeDialog 0;
			};
		};
		
		// Sends the group's selected profession to the server.
		// Called by the .hpp-dialog when the OK button is pressed.
		public static method OnOkButtonPressed()
		{
			ProfessionDialog.SelectedProfession = call _self.GetSelectedProfessionTypeFromCombo;
			["SquadHandlerReciever.SetSquadProfession", [group player, ProfessionDialog.SelectedProfession]] call Remote.Invoke;
		};		
		
		// Called by the .hpp-dialog when the Cancel button is pressed.
		public static method OnCancelButtonPressed()
		{
			call _self.CloseDialog;
		};		
	};
};
